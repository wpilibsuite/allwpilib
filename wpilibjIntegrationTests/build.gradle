plugins {
    id 'java'
    id 'application'
}

ext {
    useJava = true
    useCpp = false
    skipDev = true
}

apply from: "${rootDir}/shared/opencv.gradle"

application {
    mainClass = 'edu.wpi.first.wpilibj.test.AntJunitLauncher'
}

apply plugin: 'com.gradleup.shadow'

repositories {
    maven {
        url = 'https://frcmaven.wpi.edu/artifactory/ex-mvn'
    }
}

dependencies {
    implementation project(':wpilibj')
    implementation project(':wpimath')
    implementation project(':hal')
    implementation project(':wpiutil')
    implementation project(':wpinet')
    implementation project(':ntcore')
    implementation project(':cscore')
    implementation project(':cameraserver')
    implementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    implementation 'com.googlecode.junit-toolbox:junit-toolbox:2.4'
    implementation 'org.apache.ant:ant:1.10.12'
    implementation 'org.apache.ant:ant-junit:1.10.12'
}

build.dependsOn shadowJar

def testOutputFolder = file("${project(':').buildDir}/integrationTestFiles")

task copyWpilibJIntegrationTestJarToOutput(type: Copy) {
    destinationDir testOutputFolder
    dependsOn shadowJar
    inputs.file shadowJar.archiveFile
    from(shadowJar) {
        into 'java'
    }
}

build.dependsOn copyWpilibJIntegrationTestJarToOutput
