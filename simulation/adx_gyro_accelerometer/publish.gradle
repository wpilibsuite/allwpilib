apply plugin: 'maven-publish'
apply plugin: 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin'

if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
    }
}

def pubVersion = ''
if (project.hasProperty("publishVersion")) {
    pubVersion = project.publishVersion
} else {
    pubVersion = WPILibVersion.version
}

def baseArtifactId = 'halsim-adx_gyro_accelerometer'
def artifactGroupId = 'edu.wpi.first.halsim'

def outputsFolder = file("$project.buildDir/outputs")

task cppHeadersZip(type: Zip) {
    destinationDir = outputsFolder
    baseName = 'halsim-adx_gyro_accelerometer'
    classifier = "headers"

    from(licenseFile) {
        into '/'
    }

    from('src/main/native/include') {
        into '/'
    }
}

task cppSourcesZip(type: Zip) {
    destinationDir = outputsFolder
    baseName = 'halsim-adx_gyro_accelerometer'
    classifier = "sources"

    from(licenseFile) {
        into '/'
    }

    from('src/main/native/cpp') {
        into '/'
    }
}

model {
    publishing {
        def libSpec
        $.components.each {
            if (it in NativeLibrarySpec) {
                $.binaries.each {
                    if (it in SharedLibraryBinarySpec) {
                        libSpec = it.sharedLibraryFile
                    }
                }
            }
        }

        publications {
            cpp(MavenPublication) {
                artifact libSpec
                artifactId = baseArtifactId
                groupId artifactGroupId
                version pubVersion

                artifact cppHeadersZip
                artifact cppSourcesZip
            }
        }
    }
}
