if (project.hasProperty('onlylinuxathena') || project.hasProperty('onlylinuxsystemcore')) {
    return;
}

description = "WebSocket Server Extension"

ext {
    includeWpiutil = true
    pluginName = 'halsim_ws_server'
}

apply plugin: 'google-test-test-suite'


ext {
    staticGtestConfigs = [:]
}

staticGtestConfigs["${pluginName}Test"] = []
apply from: "${rootDir}/shared/googletest.gradle"

apply from: "${rootDir}/shared/plugins/setupBuild.gradle"

model {
    testSuites {
        "${pluginName}Test"(GoogleTestTestSuiteSpec) {
            for(NativeComponentSpec c : $.components) {
                if (c.name == pluginName) {
                    testing c
                    break
                }
            }
            sources.cpp {
                source {
                    srcDirs 'src/test/native/cpp'
                    include '**/*.cpp'
                }
                exportedHeaders {
                    srcDirs 'src/test/native/include', 'src/main/native/cpp'
                }
            }
        }
    }

    binaries {
        all {
            if (it.targetPlatform.name == nativeUtils.wpi.platforms.systemcore) {
                it.buildable = false
                return
            }

            project(':hal').addHalDependency(it, 'shared')
            project(':ntcore').addNtcoreDependency(it, 'shared')
            lib project: ':wpinet', library: 'wpinet', linkage: 'shared'
            lib project: ':wpiutil', library: 'wpiutil', linkage: 'shared'
            lib project: ":simulation:halsim_ws_core", library: "halsim_ws_core", linkage: "static"
        }

        withType(GoogleTestTestSuiteBinarySpec) {
            project(':hal').addHalDependency(it, 'shared')
            project(':ntcore').addNtcoreDependency(it, 'shared')
            lib project: ':wpinet', library: 'wpinet', linkage: 'shared'
            lib project: ':wpiutil', library: 'wpiutil', linkage: 'shared'
            lib library: pluginName, linkage: 'shared'
        }
    }
}
