load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@rules_proto//proto:defs.bzl", "proto_lang_toolchain")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")

# bazel build //:requirements.lock
compile_pip_requirements(
    name = "requirements",
    extra_args = ["--allow-unsafe"],
    requirements_in = "requirements.txt",
    requirements_txt = "requirements_lock.txt",
    tags = [
        "no-bullseye",
        "no-raspi",
        "no-roborio",
    ],
)

alias(
    name = "quickbuf_protoc",
    actual = select({
        "@bazel_tools//src/conditions:darwin": "@quickbuffer_protoc_osx//file",
        "@bazel_tools//src/conditions:windows": "@quickbuffer_protoc_windows//file",
        "@rules_bzlmodrio_toolchains//constraints/combined:is_linux": "@quickbuffer_protoc_linux//file",
    }),
    visibility = ["//visibility:public"],
)

proto_lang_toolchain(
    name = "quickbuf_toolchain",
    command_line = "--quickbuf_out=gen_descriptors=true:$(OUT)",
    plugin = ":quickbuf_protoc",
    plugin_format_flag = "--plugin=protoc-gen-quickbuf=%s",
    runtime = "@maven//:us_hebi_quickbuf_quickbuf_runtime",
    visibility = ["//visibility:public"],
)

proto_lang_toolchain(
    name = "proto_cc_toolchain",
    command_line = "--cpp_out=$(OUT)",
    runtime = "//wpiutil/src/main/native:wpiutil.static",
    visibility = ["//visibility:public"],
)

write_source_files(
    name = "write_all",
    additional_update_targets = [
        "//wpimath:write_wpimath",
        "//ntcore:write_ntcore",
    ],
)
