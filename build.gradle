buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    }
}

def enableSimulation = hasProperty('enableSimulation')

if (!hasProperty('repo')) {
    ext.repo = 'development'
}

def repoBaseUrl = "http://first.wpi.edu/FRC/roborio/maven/${repo}"
def publishUrl = "${System.getProperty('user.home')}/releases/maven/${repo}"

ext.simulationInstallDir = "$rootDir/build/install/simulation"

allprojects {
    ext.enableSimulation = enableSimulation
    ext.repo = repo

    repositories {
        mavenCentral()
        maven {
            url publishUrl
        }
        maven {
            url repoBaseUrl
        }
    }
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = "6.18"
        configFile = new File(rootDir, "styleguide/checkstyle.xml")
    }

    ext.armBuild = true

    // Disables doclint in java 8.
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    ext.setupWpilibRepo = { publishing ->
        publishing.repositories.maven {
            url = "${System.getProperty('user.home')}/releases/maven/${repo}"
        }
    }
}

apply from: 'cppSettings.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
