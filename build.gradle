def enableSimulation = hasProperty('enableSimulation')

def repoBaseUrl = 'http://first.wpi.edu/FRC/roborio/maven'
def publishUrl = "${System.getProperty('user.home')}/releases/maven/"

// If we have the repo property, we want to delete the cached version of the networktables
// dependency and redownload it from the given repo
if (!hasProperty('repo')) {
    ext.repo = 'development'
}

allprojects {
    ext.enableSimulation = enableSimulation
    ext.repo = repo
    ext.useExtension = 'release' != repo

    repositories {
        mavenCentral()
        maven {
            url publishUrl
        }
        mavenLocal()
        maven {
            url repoBaseUrl
        }
    }

    ext.getClassifier = {
        return useExtension ? "-$repo" : ''
    }
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    ext.armBuild = true

    // Disables doclint in java 8.
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    ext.setupWpilibRepo = { publishing ->
        publishing.repositories.maven {
            url = "${System.getProperty('user.home')}/releases/maven/"
        }
    }
}

apply from: 'cppSettings.gradle'