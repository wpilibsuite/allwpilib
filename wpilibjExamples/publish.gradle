apply plugin: 'maven-publish'

def baseExamplesArtifactId = 'examples'
def baseTemplatesArtifactId = 'templates'
def baseCommandsArtifactId = 'commands'
def artifactGroupId = 'org.wpilib.wpilibj'

def examplesZipBaseName = '_GROUP_org_wpilib_wpilibj_ID_examples_CLS'
def templatesZipBaseName = '_GROUP_org_wpilib_wpilibj_ID_templates_CLS'
def commandsZipBaseName = '_GROUP_org_wpilib_wpilibj_ID_commands_CLS'

def outputsFolder = file("$project.buildDir/outputs")

task javaExamplesZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = examplesZipBaseName

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/org/wpilib/examples') {
        into 'examples'
    }

    from('src/test/java/org/wpilib/examples') {
        into 'examples_test'
    }
}

task javaTemplatesZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = templatesZipBaseName

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/org/wpilib/templates') {
        into 'templates'
    }

    from('src/test/java/org/wpilib/templates') {
        into 'templates_test'
    }
}

task javaCommandsZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = commandsZipBaseName

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/org/wpilib/commands') {
        into 'commands'
    }

    from('src/test/java/org/wpilib/commands') {
        into 'commands_test'
    }
}

build.dependsOn javaTemplatesZip
build.dependsOn javaExamplesZip
build.dependsOn javaCommandsZip

addTaskToCopyAllOutputs(javaTemplatesZip)
addTaskToCopyAllOutputs(javaExamplesZip)
addTaskToCopyAllOutputs(javaCommandsZip)

publishing {
    publications {
        examples(MavenPublication) {
            artifact javaExamplesZip

            artifactId = baseExamplesArtifactId
            groupId = artifactGroupId
            version = wpilibVersioning.version.get()
        }

        templates(MavenPublication) {
            artifact javaTemplatesZip

            artifactId = baseTemplatesArtifactId
            groupId = artifactGroupId
            version = wpilibVersioning.version.get()
        }

        commands(MavenPublication) {
            artifact javaCommandsZip

            artifactId = baseCommandsArtifactId
            groupId = artifactGroupId
            version = wpilibVersioning.version.get()
        }
    }
}
