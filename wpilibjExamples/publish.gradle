apply plugin: 'maven-publish'

def baseExamplesArtifactId = 'examples'
def baseTemplatesArtifactId = 'templates'
def baseCommandsArtifactId = 'commands'
def artifactGroupId = 'edu.wpi.first.wpilibj'

def examplesZipBaseName = '_GROUP_edu_wpi_first_wpilibj_ID_examples_CLS'
def templatesZipBaseName = '_GROUP_edu_wpi_first_wpilibj_ID_templates_CLS'
def commandsZipBaseName = '_GROUP_edu_wpi_first_wpilibj_ID_commands_CLS'

def outputsFolder = file("$project.buildDir/outputs")

task javaExamplesZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = examplesZipBaseName

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/edu/wpi/first/wpilibj/examples') {
        into 'examples'
    }
}

task javaTemplatesZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = templatesZipBaseName

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/edu/wpi/first/wpilibj/templates') {
        into 'templates'
    }
}

task javaCommandsZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = commandsZipBaseName

    from(licenseFile) {
        into '/'
    }

    from('src/main/java/edu/wpi/first/wpilibj/commands') {
        into 'commands'
    }
}

build.dependsOn javaTemplatesZip
build.dependsOn javaExamplesZip
build.dependsOn javaCommandsZip

addTaskToCopyAllOutputs(javaTemplatesZip)
addTaskToCopyAllOutputs(javaExamplesZip)
addTaskToCopyAllOutputs(javaCommandsZip)

publishing {
    publications {
        examples(MavenPublication) {
            artifact javaExamplesZip

            artifactId = baseExamplesArtifactId
            groupId artifactGroupId
            version wpilibVersioning.version.get()
        }

        templates(MavenPublication) {
            artifact javaTemplatesZip

            artifactId = baseTemplatesArtifactId
            groupId artifactGroupId
            version wpilibVersioning.version.get()
        }

        commands(MavenPublication) {
            artifact javaCommandsZip

            artifactId = baseCommandsArtifactId
            groupId artifactGroupId
            version wpilibVersioning.version.get()
        }
    }
}
