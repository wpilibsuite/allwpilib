load("@rules_cc//cc:defs.bzl", "cc_binary")
load("@rules_jvm_external//:defs.bzl", "maven_export")
load("@rules_pkg//:mappings.bzl", "pkg_files")
load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_library", "wpilib_cc_static_library")
load("//shared/bazel/rules:packaging.bzl", "package_static_cc_project")
load("//shared/bazel/rules:publishing.bzl", "architectures_pkg_zip", "host_architectures", "platform_prefix")
load("//shared/bazel/rules/gen:gen-resources.bzl", "generate_resources")
load("//shared/bazel/rules/gen:gen-version-file.bzl", "generate_version_file")

wpilib_cc_library(
    name = "glass",
    srcs = glob(["src/lib/native/cpp/**/*.cpp"]),
    hdrs = glob(["src/lib/native/include/**"]),
    hdrs_pkg_root = "src/lib/native/include",
    include_license_files = True,
    srcs_pkg_root = "src/lib/native/cpp",
    strip_include_prefix = "src/lib/native/include",
    tags = [
        "wpi-cpp-gui",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//fieldImages",
        "//wpigui",
        "//wpimath",
        "//wpiutil",
    ],
)

wpilib_cc_static_library(
    name = "static/glass",
    static_deps = [
        "//wpigui:static/wpigui",
        "//wpimath:static/wpimath",
        "//wpiutil:static/wpiutil",
        "//fieldImages:static/fieldImages",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":glass",
    ],
)

wpilib_cc_library(
    name = "glassnt",
    srcs = glob(["src/libnt/native/cpp/*.cpp"]),
    hdrs = glob(["src/libnt/native/include/**/*.h"]),
    hdrs_pkg_root = "src/libnt/native/include",
    include_license_files = True,
    srcs_pkg_root = "src/libnt/native/cpp",
    strip_include_prefix = "src/libnt/native/include",
    tags = [
        "wpi-cpp-gui",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":glass",
        "//ntcore",
    ],
)

wpilib_cc_static_library(
    name = "static/glassnt",
    static_deps = [
        ":static/glass",
        "//ntcore:static/ntcore",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":glassnt",
    ],
)

generate_version_file(
    name = "generate-version",
    output_file = "WPILibVersion.cpp",
    template = "src/app/generate/WPILibVersion.cpp.in",
)

generate_resources(
    name = "generate-resources",
    namespace = "glass",
    prefix = "GLASS",
    resource_files = glob(["src/app/native/resources/*"]),
)

cc_binary(
    name = "glassApp",
    srcs = [
        ":generate-resources",
        ":generate-version",
    ] + glob(["src/app/native/cpp/**"]),
    linkopts = select({
        "@platforms//os:windows": ["-SUBSYSTEM:WINDOWS"],
        "//conditions:default": [],
    }),
    tags = [
        "wpi-cpp-gui",
    ],
    deps = [
        ":glassnt",
    ],
)

package_static_cc_project(
    name = "glass",
    architectures = host_architectures,
    maven_artifact_name = "libglass",
    maven_group_id = "edu.wpi.first.glass",
)

package_static_cc_project(
    name = "glassnt",
    architectures = host_architectures,
    maven_artifact_name = "libglassnt",
    maven_group_id = "edu.wpi.first.glass",
)

pkg_files(
    name = "glassapp-files",
    srcs = [
        ":glassApp",
    ],
    prefix = platform_prefix(""),
    renames = {
        "glassApp": "glass",
    },
)

architectures_pkg_zip(
    name = "glassapp_zip",
    srcs = [
        ":glassapp-files",
        "//:license_pkg_files",
    ],
    architectures = {
        "@rules_bzlmodrio_toolchains//platforms/linux_x86_64": "linux-x86-64",
    },
    compilation_modes = ["opt"],
)

maven_export(
    name = "glassapp_publish",
    classifier_artifacts = {
        "linuxx86-64": ":glassapp_zip-opt-linux-x86-64",
    },
    maven_coordinates = "edu.wpi.first.tools:Glass:$(WPILIB_VERSION)",
    visibility = ["//visibility:public"],
)
