def opencvVersion = '3.4.7-2'

if (project.hasProperty('useCpp') && project.useCpp) {
    model {
        binaries {
            withType(NativeBinarySpec).all {
                def binary = it
                project.sharedCvConfigs.each {
                    if (binary.component.name == it.key) {
                        nativeUtils.useRequiredLibrary(binary, 'opencv_shared')
                    }
                }
                project.staticCvConfigs.each {
                    if (binary.component.name == it.key) {
                        nativeUtils.useRequiredLibrary(binary, 'opencv_static')
                    }
                }
            }
        }
    }
}

if (project.hasProperty('useJava') && project.useJava) {
    dependencies {
        compile "edu.wpi.first.thirdparty.frc2020.opencv:opencv-java:${opencvVersion}"
        if (!project.hasProperty('skipDev') || !project.skipDev) {
            devCompile "edu.wpi.first.thirdparty.frc2020.opencv:opencv-java:${opencvVersion}"
        }
        if (project.hasProperty('useDocumentation') && project.useDocumentation) {
            javaSource "edu.wpi.first.thirdparty.frc2020.opencv:opencv-java:${opencvVersion}:sources"
        }
    }
}
