load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_static_and_shared_library")
load("//shared/bazel/rules/gen:gen-resources.bzl", "generate_resources")
load("//shared/bazel/rules/jni:cc_jni.bzl", "wpilib_jni_cc_library")

generate_resources(
    name = "generate-resources",
    namespace = "dlt",
    prefix = "DLT",
    resource_files = glob(["resources/*"]),
)

filegroup(
    name = "native-srcs",
    srcs = ["generate-resources"] + select({
        "@bazel_tools//src/conditions:darwin": glob(["macOS/*"]),
        "@bazel_tools//src/conditions:linux_x86_64": glob(["linux/*"]),
        "@bazel_tools//src/conditions:windows": glob(["windows/*"]),
        # "//shared/bazel/toolchains/conditions:raspbian": glob(["linux/*"]),
        # "//shared/bazel/toolchains/conditions:bionic": glob(["linux/*"]),
        "@bazelrio//constraints/is_roborio:roborio": glob(["linux/*"]),
    }),
)

wpilib_cc_static_and_shared_library(
    name = "wpinet",
    srcs = glob(
        ["cpp/**"],
        exclude = ["cpp/jni/**"],
    ) + [
        "//wpinet/src/main/native/thirdparty/libuv:libuv-srcs",
        "//wpinet/src/main/native/thirdparty/tcpsockets:tcpsockets-srcs",
    ] + ["native-srcs"],
    hdrs = glob(["include/**/*"]),
    includes = ["include"],
    standard_deps = [
        "//wpinet/src/main/native/thirdparty/libuv:libuv-headers",
        "//wpinet/src/main/native/thirdparty/tcpsockets:tcpsockets-headers",
    ],
    strip_include_prefix = "include",
    visibility = ["//visibility:public"],
    wpi_maybe_shared_deps = [
        "//wpiutil/src/main/native:wpiutil",
    ],
)

wpilib_jni_cc_library(
    name = "wpinetjni",
    srcs = glob([
        "cpp/jni/**",
        "cpp/**/*.h",
    ]),
    java_dep = "//wpinet/src/main/java/edu/wpi/first/net:wpinet",
    visibility = ["//visibility:public"],
    wpi_maybe_shared_deps = [
        ":wpinet",
    ],
)
