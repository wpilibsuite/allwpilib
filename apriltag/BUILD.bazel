load("@allwpilib_pip_deps//:requirements.bzl", "requirement")
load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load("@rules_java//java:defs.bzl", "java_binary")
load("@rules_pkg//:mappings.bzl", "pkg_files")
load("@rules_python//python:defs.bzl", "py_binary")
load("//apriltag:robotpy_native_build_info.bzl", "define_native_wrapper")
load("//apriltag:robotpy_pybind_build_info.bzl", "apriltag_extension", "define_pybind_library")
load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_library", "wpilib_cc_shared_library", "wpilib_cc_static_library")
load("//shared/bazel/rules:java_rules.bzl", "wpilib_java_junit5_test")
load("//shared/bazel/rules:jni_rules.bzl", "wpilib_jni_cc_library", "wpilib_jni_java_library")
load("//shared/bazel/rules:packaging.bzl", "package_default_jni_project")
load("//shared/bazel/rules/gen:gen-resources.bzl", "generate_resources")
load("//shared/bazel/rules/robotpy:build_info_gen.bzl", "generate_robotpy_native_wrapper_build_info", "generate_robotpy_pybind_build_info")
load("//shared/bazel/rules/robotpy:pytest_util.bzl", "robotpy_py_test")

filegroup(
    name = "doxygen-files",
    srcs = glob([
        "src/main/native/include/**/*",
        "src/main/native/thirdparty/apriltag/include/**/*",
    ]),
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "thirdparty-apriltag-src-pkg",
    srcs = glob(["src/main/native/thirdparty/apriltag/include/**"]),
    strip_prefix = "src/main/native/thirdparty/apriltag/include",
)

cc_library(
    name = "thirdparty-apriltag",
    srcs = glob(["src/main/native/thirdparty/apriltag/src/**"]),
    hdrs = glob(["src/main/native/thirdparty/apriltag/include/**"]),
    copts = select({
        "@platforms//os:osx": [
            "-Wno-format-nonliteral",
            "-Wno-gnu-zero-variadic-macro-arguments",
            "-Wno-uninitialized",
            "-Wno-sign-compare",
            "-Wno-type-limits",
        ],
        "@platforms//os:windows": [
            "/wd4005",
            "/wd4018",
            "/wd4244",
            "/wd4267",
            "/wd4996",
        ],
        "@rules_bzlmodrio_toolchains//constraints/combined:is_linux": [
            "-Wno-format-nonliteral",
            "-Wno-maybe-uninitialized",
            "-Wno-sign-compare",
            "-Wno-type-limits",
        ],
    }),
    includes = ["src/main/native/thirdparty/apriltag/include/common"],
    strip_include_prefix = "src/main/native/thirdparty/apriltag/include",
)

generate_resources(
    name = "generate-resources",
    namespace = "wpi::apriltag",
    prefix = "APRILTAG",
    resource_files = glob(["src/main/native/resources/**"]),
)

wpilib_cc_library(
    name = "apriltag",
    srcs = [":generate-resources"] + glob(
        ["src/main/native/cpp/**"],
        exclude = ["src/main/native/cpp/jni/**"],
    ),
    hdrs = glob(["src/main/native/include/**/*"]),
    extra_hdr_pkg_files = [":thirdparty-apriltag-src-pkg"],
    extra_src_pkg_files = [":apriltag-java-jni-hdrs-pkg"],
    local_defines = ["WPILIB_EXPORTS"],
    strip_include_prefix = "src/main/native/include",
    visibility = ["//visibility:public"],
    deps = [
        ":thirdparty-apriltag",
        "//wpimath",
        "//wpiutil",
    ],
)

wpilib_cc_shared_library(
    name = "shared/apriltag",
    auto_export_windows_symbols = False,
    dynamic_deps = [
        "//wpimath:shared/wpimath",
        "//wpiutil:shared/wpiutil",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":apriltag",
        ":thirdparty-apriltag",
    ],
)

wpilib_cc_static_library(
    name = "static/apriltag",
    static_deps = [
        "//wpimath:static/wpimath",
        "//wpiutil:static/wpiutil",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":apriltag",
    ],
)

wpilib_jni_cc_library(
    name = "apriltagjni",
    srcs = glob(["src/main/native/cpp/jni/**"]),
    java_dep = ":apriltag-java",
    visibility = ["//visibility:public"],
    deps = [
        ":apriltag",
    ],
)

wpilib_cc_shared_library(
    name = "shared/apriltagjni",
    auto_export_windows_symbols = False,
    dynamic_deps = [
        ":shared/apriltag",
        "//wpimath:shared/wpimath",
        "//wpiutil:shared/wpiutil",
    ],
    use_debug_name = False,
    visibility = ["//visibility:public"],
    deps = [":apriltagjni"],
)

wpilib_jni_java_library(
    name = "apriltag-java",
    srcs = glob(["src/main/java/**/*.java"]),
    extra_source_pkgs = ["resources"],
    maven_artifact_name = "apriltag-java",
    maven_group_id = "org.wpilib.apriltag",
    native_libs = [":apriltagjni"],
    resource_strip_prefix = "apriltag/src/main/native/resources",
    resources = glob(["src/main/native/resources/**"]),
    visibility = ["//visibility:public"],
    deps = [
        "//wpimath:wpimath-java",
        "//wpiutil:wpiutil-java",
        "@bzlmodrio-opencv//libraries/java/opencv",
        "@maven//:com_fasterxml_jackson_core_jackson_annotations",
        "@maven//:com_fasterxml_jackson_core_jackson_core",
        "@maven//:com_fasterxml_jackson_core_jackson_databind",
    ],
)

pkg_files(
    name = "resources",
    srcs = glob(["src/main/native/resources/**"]),
    strip_prefix = "src/main/native/resources/",
)

cc_test(
    name = "gtest_tests",
    size = "small",
    srcs = glob(["src/test/native/cpp/**"]),
    tags = [
        "no-asan",
    ],
    deps = [
        ":apriltag",
        "//thirdparty/googletest",
    ],
)

wpilib_java_junit5_test(
    name = "java_tests",
    srcs = glob(["src/test/java/**/*.java"]),
    resource_strip_prefix = "apriltag/src/test/resources",
    resources = glob(["src/test/resources/**"]),
    deps = [
        ":apriltag-java",
        "//wpimath:wpimath-java",
        "//wpiutil:wpiutil-java",
        "@bzlmodrio-opencv//libraries/java/opencv",
        "@maven//:com_fasterxml_jackson_core_jackson_databind",
    ],
)

cc_binary(
    name = "DevMain-Cpp",
    srcs = ["src/dev/native/cpp/main.cpp"],
    deps = [
        ":apriltag",
    ],
)

java_binary(
    name = "DevMain-Java",
    srcs = ["src/dev/java/org/wpilib/vision/apriltag/DevMain.java"],
    main_class = "org.wpilib.vision.apriltag.DevMain",
    deps = [
        ":apriltag-java",
    ],
)

py_binary(
    name = "convert_apriltag_layouts",
    srcs = ["convert_apriltag_layouts.py"],
    target_compatible_with = select({
        "@rules_bzlmodrio_toolchains//constraints/is_systemcore:systemcore": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

package_default_jni_project(
    name = "apriltag",
    maven_artifact_name = "apriltag-cpp",
    maven_group_id = "org.wpilib.apriltag",
)

generate_robotpy_native_wrapper_build_info(
    name = "robotpy-native-apriltag-generator",
    pyproject_toml = "src/main/python/native-pyproject.toml",
    third_party_dirs = ["apriltag"],
)

define_native_wrapper(
    name = "robotpy-native-apriltag",
    pyproject_toml = "src/main/python/native-pyproject.toml",
)

PYBIND_PKGCFG_DEPS = [
    "//apriltag:native/apriltag/robotpy-native-apriltag.pc",
    "//wpimath:native/wpimath/robotpy-native-wpimath.pc",
    "//wpimath:robotpy-wpimath.generated_pkgcfg_files",
    "//wpiutil:native/wpiutil/robotpy-native-wpiutil.pc",
    "//wpiutil:robotpy-wpiutil.generated_pkgcfg_files",
]

generate_robotpy_pybind_build_info(
    name = "robotpy-apriltag-generator",
    additional_srcs = [":robotpy-native-apriltag.copy_headers"],
    package_root_file = "src/main/python/robotpy_apriltag/__init__.py",
    pkgcfgs = PYBIND_PKGCFG_DEPS,
    yaml_files = glob(["src/main/python/semiwrap/*.yml"]),
)

apriltag_extension(
    srcs = ["src/main/python/robotpy_apriltag/src/main.cpp"],
    includes = [
        "src/main/python/datalog/",
    ],
)

define_pybind_library(
    name = "robotpy-apriltag",
    pkgcfgs = PYBIND_PKGCFG_DEPS,
)

robotpy_py_test(
    "python_tests",
    srcs = glob(["src/test/python/**/*.py"]),
    data = glob([
        "src/test/python/*.png",
        "src/test/python/*.jpg",
    ]),
    deps = [
        ":robotpy-apriltag",
        requirement("pytest"),
        requirement("opencv-python"),
    ],
)
